--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: HelperScripts
Element-Type: context
Element-Version: 2.0
Id: _jPStUFRJEeaXLqlXvYQIgg
Runtime-Version: 2.1.0.201606221726
Save-Time: 7/28/16 7:14 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// Return the dashboard view. Open it if necessary.
proc getDashboard [] {
	try {
		get-view "Gerrit Dashboard (EGerrit)"
	} -catch {
		get-menu "Window/Show View/Other..." | click
		with [get-window "Show View"] {
			get-tree | select "Git/Gerrit Dashboard (EGerrit)"
			get-button OK | click
		}
		get-view "Gerrit Dashboard (EGerrit)"
	}
}

// Close the dashboard view
proc closeDashboard [] {
	try {
		get-view "Gerrit Dashboard (EGerrit)" | close
	}
}

// Enter a query in the dashboard search dialog and execute it. The query is executed against the default server.
proc runQuery [val query -input] {
	getDashboard | get-button "Select current Gerrit repository" | click -arrow
	get-menu $serverName | click
	get-view "Gerrit Dashboard (EGerrit)" | get-combo | set-text $query
	get-button Search | click
}

// Run the query and open the review editor on the first match
proc runQueryAndOpenEditor [val query -input] {
	runQuery $query
	wait -ms 500
	with [getDashboard] {
		with [get-table] {
			get-cell -row 0 -column 1 | double-click
		}
	}
}

proc assertStatusInDashboard [val expectedStatus -input] {
	getDashboard | get-table | get-item false | get-property "columns[3]" | equals $expectedStatus | verify-true
}

proc assertSubjectInDashboard [val expectedSubject -input] {
	getDashboard | get-table | get-item false | get-property "columns[2]" | equals $expectedSubject | verify-true
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
